# Adapted from the Linux kernel's Documentation/iostat.txt
package diskstats

type Grammar Peg {}

# Both /proc/diskstats and sysfs use the same source for the information
# and so should not differ.
diskstats <- (stats ('\n'/!.))* !.

# If you look at
# /proc/diskstats, the eleven fields will be preceded by the major and
# minor device numbers, and device name.
device <-		device_major
	_spacing	device_minor
	_spacing	device_name
device_major <- [0-9]+
device_minor <- [0-9]+
device_name <- [a-zA-Z0-9]+

#
# Each set of stats only applies to the indicated device; if you want
# system-wide stats you'll have to find all the devices and sum them all up.
stats <-_spacing?	device
	_spacing	statistics

# In 2.4, the statistics fields are those after the device name.
statistics <-		reads_completed
	_spacing	reads_merged
	_spacing	sectors_read
	_spacing	ms_reading
	_spacing	writes_completed
	_spacing	writes_merged
	_spacing	sectors_written
	_spacing	ms_writing
	_spacing	ios_in_progress
	_spacing	ms_io
	_spacing	weighted_ms_io

# Field  1 -- # of reads completed
# This is the total number of reads completed successfully.
reads_completed <- [0-9]+

# Field  2 -- # of reads merged, field 6 -- # of writes merged
# Reads and writes which are adjacent to each other may be merged for
# efficiency.  Thus two 4K reads may become one 8K read before it is
# ultimately handed to the disk, and so it will be counted (and queued)
# as only one I/O.  This field lets you know how often this was done.
reads_merged <- [0-9]+

# Field  3 -- # of sectors read
# This is the total number of sectors read successfully.
sectors_read <- [0-9]+

# Field  4 -- # of milliseconds spent reading
# This is the total number of milliseconds spent by all reads (as
# measured from __make_request() to end_that_request_last()).
ms_reading <- [0-9]+

# Field  5 -- # of writes completed
# This is the total number of writes completed successfully.
writes_completed <- [0-9]+

# Field  6 -- # of writes merged
# See the description of field 2.
writes_merged <- [0-9]+

# Field  7 -- # of sectors written
# This is the total number of sectors written successfully.
sectors_written <- [0-9]+

# Field  8 -- # of milliseconds spent writing
# This is the total number of milliseconds spent by all writes (as
# measured from __make_request() to end_that_request_last()).
ms_writing <- [0-9]+

# Field  9 -- # of I/Os currently in progress
# The only field that should go to zero. Incremented as requests are
# given to appropriate struct request_queue and decremented as they finish.
ios_in_progress <- [0-9]+

#Field 10 -- # of milliseconds spent doing I/Os
# This field increases so long as field 9 is nonzero.
ms_io <- [0-9]+

# Field 11 -- weighted # of milliseconds spent doing I/Os
# This field is incremented at each I/O start, I/O completion, I/O
# merge, or read of these stats by the number of I/Os in progress
# (field 9) times the number of milliseconds spent doing I/O since the
# last update of this field.  This can provide an easy measure of both
# I/O completion time and the backlog that may be accumulating.
weighted_ms_io <- [0-9]+

_spacing <- [ \t]+
